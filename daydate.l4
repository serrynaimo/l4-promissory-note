ยง `Day & Date Definitions and Helpers`

-- Some basic functions to enable math with dates and days following ISO 8601
-- Functions return days since 1st January 1970, week begins on Monday
-- Check the test/daydate.l4 file to see examples

GIVEN day   IS A NUMBER
      month IS A NUMBER
      year  IS A NUMBER
`Date` MEANS
    Month month year PLUS (day MINUS 1)


GIVEN year  IS A NUMBER
`Year` MEANS
    Month 1 year

GIVEN month IS A NUMBER
      year  IS A NUMBER
`Month` MEANS
    IF    month LESS THAN 1
    THEN  `add up month days` (m PLUS (`Months in a year` TIMES n)) (y MINUS n)
    ELSE  `add up month days` m y
    WHERE
        y MEANS FLOOR year MINUS 1968 -- using 1968 so that leap year calculations are aligned. It's divisble by 4.
        m MEANS FLOOR month MINUS 1
        n MEANS FLOOR (1 PLUS (m TIMES -1 DIVIDED BY `Months in a year`))

        GIVEN rm IS A NUMBER
              ry IS A NUMBER
        `add up month days` MEANS
            IF    rm LESS THAN 1
            THEN  ((ry MINUS 2) TIMES 365) -- Add the days within the years prior with 2 years subtracted to get to 1970
                      PLUS  FLOOR ((ry MINUS 1) DIVIDED BY 4)     -- consider leap years
                      MINUS FLOOR ((ry MINUS 33) DIVIDED BY 100)
                      PLUS  FLOOR ((ry MINUS 33) DIVIDED BY 400)
            ELSE  `Days in month` rm ry
                      PLUS `add up month days` (rm MINUS 1) ry -- Recursively add all days of months that year

GIVEN month IS A NUMBER
      year  IS A NUMBER
`Days in month` MEANS
    IF       m EQUALS 4   -- April
          OR m EQUALS 6   -- June
          OR m EQUALS 9   -- September
          OR m EQUALS 11  -- November
    THEN  30
    ELSE  IF    m EQUALS 2
          THEN  IF    y MODULO 4 EQUALS 0
                THEN  29  -- leap year
                ELSE  28
          ELSE  31
    WHERE
        m MEANS 1 PLUS (FLOOR month MINUS 1)
                    MINUS (FLOOR ((month MINUS 1)
                        DIVIDED BY `Months in a year`))
                            TIMES `Months in a year`
        y MEANS FLOOR year
                    PLUS FLOOR (month DIVIDED BY `Months in a year`)

GIVEN year  IS A NUMBER
`Days in year` MEANS
    IF   `is leap year` year
    THEN 366
    ELSE 365

GIVEN days  IS A NUMBER
`Weekday of` MEANS
    days MODULO `Days in a week`

GIVEN month  IS A NUMBER
      year   IS A NUMBER
`Weekday of 1st day of month` MEANS
    `Weekday of`
        (Date 1 month year)

GIVEN year  IS A NUMBER
`Weekday of 1st day of year` MEANS
    `Weekday of` (Year year)

GIVEN week  IS A NUMBER
      year  IS A NUMBER
`Week` MEANS
    Year year
        PLUS Monday MINUS (`Weekday of 1st day of year` year)
            PLUS ((week MINUS 1) TIMES `Days in a week`)

GIVEN year  IS A NUMBER
`is leap year` MEANS
            y MODULO 4   EQUALS 0
    AND NOT y MODULO 100 EQUALS 0
    OR      y MODULO 400 EQUALS 0
    WHERE
      y MEANS FLOOR year

GIVEN year  IS A NUMBER
`Weeks in year` MEANS
    IF        h EQUALS 5
          OR (h EQUALS 4 AND `is leap year` year)
    THEN  53
    ELSE  52
    WHERE
        y MEANS FLOOR year MINUS 1
        k MEANS y MODULO 100
        j MEANS FLOOR (y DIVIDED BY 100)
        h MEANS (37 PLUS k
                    PLUS FLOOR (k DIVIDED BY 4)
                    PLUS FLOOR (j DIVIDED BY 4)
                    PLUS (5 TIMES j))
                MODULO `Days in a week`

GIVEN days  IS A NUMBER
`is weekend` MEANS
        w EQUALS Saturday
    OR  w EQUALS Sunday
    WHERE
        d MEANS FLOOR days
        w MEANS `Weekday of` d

GIVEN days  IS A NUMBER
`is weekday` MEANS
    NOT `is weekend` days


-- Some helper variables
`Months in a year` MEANS 12
`Days in a year` MEANS 365.2425
`Days in a month` MEANS `Days in a year` DIVIDED BY `Months in a year`
`Days in a week` MEANS 7

`Monday` AKA `Mon` MEANS 4
`Tuesday` AKA `Tue` MEANS 5
`Wednesday` AKA `Wed` MEANS 6
`Thursday` AKA `Thu` MEANS 0
`Friday` AKA `Fri` MEANS 1
`Saturday` AKA `Sat` MEANS 2
`Sunday` AKA `Sun` MEANS 3

-- And convenience functions for months so you can write January 1 1970
GIVEN day   IS A NUMBER
      year  IS A NUMBER
`January` AKA `Jan` MEANS
    Date day 1 year

GIVEN day   IS A NUMBER
      year  IS A NUMBER
`February` AKA `Feb` MEANS
    Date day 2 year

GIVEN day   IS A NUMBER
      year  IS A NUMBER
`March` AKA `Mar` MEANS
    Date day 3 year

GIVEN day   IS A NUMBER
      year  IS A NUMBER
`April` AKA `Apr` MEANS
    Date day 4 year

GIVEN day   IS A NUMBER
      year  IS A NUMBER
`May` MEANS
    Date day 5 year

GIVEN day   IS A NUMBER
      year  IS A NUMBER
`June` AKA `Jun` MEANS
    Date day 6 year

GIVEN day   IS A NUMBER
      year  IS A NUMBER
`July` AKA `Jul` MEANS
    Date day 7 year

GIVEN day   IS A NUMBER
      year  IS A NUMBER
`August` AKA `Aug` MEANS
    Date day 8 year

GIVEN day   IS A NUMBER
      year  IS A NUMBER
`September` AKA `Sep` MEANS
    Date day 9 year

GIVEN day   IS A NUMBER
      year  IS A NUMBER
`October` AKA `Oct` MEANS
    Date day 10 year

GIVEN day   IS A NUMBER
      year  IS A NUMBER
`November` AKA `Nov` MEANS
    Date day 11 year

GIVEN day   IS A NUMBER
      year  IS A NUMBER
`December` AKA `Dec` MEANS
    Date day 12 year
